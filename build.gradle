plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

group = 'xyz.brassgoggledcoders'
version = '0.1.0'

java {
    archivesBaseName = 'ScrapBags'
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'parchment', version: '2022.11.27-1.19.2'

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.console.level', 'debug'

            mods {
                scrapbags {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'scrapbags'

            mods {
                scrapbags {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'scrapbags', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                scrapbags {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run/gametest')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'scrapbags'

            mods {
                scrapbags {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = "Registrate"
        url = "https://maven.tterrag.com/"
        content {
            includeGroup "com.tterrag.registrate"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.2.11'

    implementation fg.deobf("com.tterrag.registrate:Registrate:MC1.19-1.1.5")
    jarJar(group: 'com.tterrag.registrate', name: 'Registrate', version: "MC1.19-1.1.5") {
        jarJar.pin(it, "MC1.19-1.1.5")
        jarJar.ranged(it, "[MC1.19,MC1.19.3)")
    }
}

jar {
    classifier = "nodep"
    manifest {
        attributes([
                "Specification-Title"     : "mccivilizations",
                "Specification-Vendor"    : "Brass Goggled Coders",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Brass Goggled Coders",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

reobf {
    jarJar { }
}

tasks.jarJar.configure {
    classifier ''
}

jar.finalizedBy('reobfJar')
tasks.jarJar.finalizedBy('reobfJarJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}